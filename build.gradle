import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java'
}

group 'cs321'
version '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.3'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task createJarGeneBankCreateBTree(type: Jar) {
    group = 'Run'
    description = 'create build/libs/GeneBankCreateBTree.jar'

    from(sourceSets.main.output) {
        include "cs321/create/**"
    }

    manifest {
        attributes 'Implementation-Title': 'GeneBankCreateBTree',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'cs321.create.GeneBankCreateBTree'
    }
    archiveBaseName = 'GeneBankCreateBTree'
    archiveVersion = ''

    // the duplicatesStrategy is required by Gradle 7
    duplicatesStrategy = DuplicatesStrategy.INCLUDE //allow duplicate *.class files in the jar

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task createJarGeneBankSearchBTree(type: Jar) {
    group = 'Run'
    description = 'create build/libs/GeneBankSearchBTree.jar'

    from(sourceSets.main.output) {
        include "cs321/search/**"
    }

    manifest {
        attributes 'Implementation-Title': 'GeneBankSearchBTree',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'cs321.search.GeneBankSearchBTree'
    }
    archiveBaseName = 'GeneBankSearchBTree'
    archiveVersion = ''

    // the duplicatesStrategy is required by Gradle 7
    duplicatesStrategy = DuplicatesStrategy.INCLUDE //allow duplicate *.class files in the jar

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task createJarGeneBankSearchDatabase(type: Jar) {
    group = 'Run'
    description = 'create build/libs/GeneBankSearchDatabase.jar'

    from(sourceSets.main.output) {
        include "cs321/search/**"
    }

    manifest {
        attributes 'Implementation-Title': 'GeneBankSearchDatabase',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'cs321.search.GeneBankSearchDatabase'
    }
    archiveBaseName = 'GeneBankSearchDatabase'
    archiveVersion = ''

    // the duplicatesStrategy is required by Gradle 7
    duplicatesStrategy = DuplicatesStrategy.INCLUDE //allow duplicate *.class files in the jar

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.createJarGeneBankCreateBTree.finalizedBy('printInstructionsToRunJarGeneBankCreateBTree')

task printInstructionsToRunJarGeneBankCreateBTree {
    doLast {
        println ''
        println '------------------------------------------------------------'
        println 'The GeneBankCreateBTree.jar was created successfully. Run:'
        println '$ java -jar build/libs/GeneBankCreateBTree.jar <arguments>'
        println '------------------------------------------------------------'
        println ''
    }

    onlyIf {
        tasks.createJarGeneBankCreateBTree.state.failure == null
    }
}

tasks.createJarGeneBankSearchBTree.finalizedBy('printInstructionsToRunJarGeneBankSearchBTree')

task printInstructionsToRunJarGeneBankSearchBTree {
    doLast {
        println ''
        println '------------------------------------------------------------'
        println 'The GeneBankSearchBTree.jar was created successfully. Run:'
        println '$ java -jar build/libs/GeneBankSearchBTree.jar <arguments>'
        println '------------------------------------------------------------'
        println ''
    }

    onlyIf {
        tasks.createJarGeneBankSearchBTree.state.failure == null
    }
}

tasks.createJarGeneBankSearchDatabase.finalizedBy('printInstructionsToRunJarGeneBankSearchDatabase')

task printInstructionsToRunJarGeneBankSearchDatabase {
    doLast {
        println ''
        println '------------------------------------------------------------'
        println 'The GeneBankSearchDatabase.jar was created successfully. Run:'
        println '$ java -jar build/libs/GeneBankSearchDatabase.jar <arguments>'
        println '------------------------------------------------------------'
        println ''
    }

    onlyIf {
        tasks.createJarGeneBankSearchDatabase.state.failure == null
    }
}

// When running
// `$ ./gradlew test`
// print to console the test outcome and a summary of the tests
// Source:
//   https://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
               TestLogEvent.PASSED,
               TestLogEvent.SKIPPED,
               TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                   TestLogEvent.FAILED,
                   TestLogEvent.PASSED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
